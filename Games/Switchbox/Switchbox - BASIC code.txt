100 FORA=54272TO54295:POKEA,0:NEXT:POKE54296,15:POKE54277,24:POKE54284,26
110 V=54276:LB=54272:HB=LB+1
120 DIMSW(4,7,1),SP$(1),LB(32,4),AR$(1),PT(4,16),SC(1,8)
130 SP$(0)="{OFF}{C= *}{RVS}{C= *}{OFF}{C= @}":SP$(1)="{C= @}{RVS}{SHIFT £}{OFF}{SHIFT £}":AR$(0)="<{SHIFT I} {DOWN}{LEFT}{LEFT}{SHIFT J}{C= W}":AR$(1)="{C= Q}{SHIFT K}{UP}{LEFT}{LEFT} {SHIFT U}>":QR=1
140 POKE53281,15:POKE53280,15:POKE646,6:TX=RND(-TI/137)
150 FORJ=1TO4:READPT(J,0):REM NAME AND GOAL
160 FORK=1TO8:READL:PT(J,K+8)=L:PT(J,9-K)=L:NEXTK,J:REM POINTS
170 DATA 10:REM ROUND 1 (EQUAL)
180 DATA 2,2,2,2,2,2,2,2
190 DATA 40:REM ROUND 2 (FIBONACCI)
200 DATA 1,2,3,5,8,13,21,34
210 DATA 20:REM ROUND 3 (ARITHMETIC)
220 DATA 2,3,4,5,6,7,8,9
230 DATA 80:REM ROUND 4 (SQUARES)
240 DATA 1,4,9,16,25,36,49,64
250 PRINT"{CLR}":INPUT"PLAYER 1";P1$
260 INPUT"PLAYER 2";P2$:P1$=LEFT$(P1$,5):P2$=LEFT$(P2$,5):PRINTP1$;" VS ";P2$
270 PRINT"IS THIS CORRECT?":POKE198,0:WAIT198,1:GETA$:IFASC(A$)<>89THEN250
280 GOSUB450:GOSUB610:REM SETUP
290 FORRR=1TO4:TX=1072+40*RR:POKETX,90:POKETX+22,90
300 GOSUB560:REM PUT SCORES AT BOTTOM
310 QR=1-QR:POKE646,6:TY=QR*20:TX=28-TY:CX=TX:CY=0
320 M$=RIGHT$(STR$(PT(RR,0)),3)+"{DOWN}{LEFT}{LEFT}{LEFT}   ":GOSUB1180
330 TX=8+TY:CX=TX:CY=QR:M$=AR$(QR):GOSUB1180
340 GOSUB770:IFSC(1-QR,RR)=>PT(RR,0)THEN360:REM END OF ROUND
350 GOTO310
360 FORJ=0TO1:FORK=5TO8:SC(J,K)=0:NEXTK,J
370 FORJ=0TO1:FORK=1TO4:GL=PT(K,0):AC=SC(J,K):SC(J,5)=SC(J,5)+AC
380 SC(J,6)=SC(J,6)-(AC=>GL)*GL:SC(J,7)=SC(J,7)+(SC(J,K)-SC(1-J,K)):NEXTK,J
390 FORJ=0TO1:FORK=6TO7:SC(J,K)=SC(J,K)+SC(J,5):NEXTK,J
400 FORJ=0TO1:FORK=5TO7:SC(J,8)=SC(J,8)+SC(J,K):NEXTK,J
410 POKE646,11:FORJ=0TO1:FORK=5TO8:Y$=STR$(SC(J,K)):L=LEN(Y$):TX=6+J*31-L
420 TY=3+K:CX=TX+(TX<20):CY=TY:M$=Y$:GOSUB1180:NEXTK,J
430 NEXTRR:REM END OF MAIN LOOP 60-499
440 POKE198,0:WAIT198,1:RUN
450 PRINT"{CLR}";:PRINTSPC(11);"{C= A}{RVS}{C= O}{OFF}";:FORJ=1TO7:PRINT"{OFF}{C= R}{RVS}{C= O}";:NEXT:PRINT"{OFF}{C= S}":LL=7
460 FORJ=0TO4:TX=9-2*J:TY=1+J*4:R$="{SHIFT -} "
470 FORK=1TO2:CX=TX:CY=TY+K-1:M$="":GOSUB1180
480 PRINT"  {RVS} {OFF} ";:GOSUB550:PRINT"{RVS} ":NEXT
490 CX=TX:CY=TY+K-1:M$="":GOSUB1180
500 PRINT" {RVS}{SHIFT £} {OFF} ";:GOSUB550:PRINT"{RVS} {C= *}":CX=TX:CY=TY+K:M$="":GOSUB1180
510 LL=LL+2:PRINT"{RVS}{SHIFT £}{OFF}{SHIFT £}";:GOSUB550:PRINT"{LEFT}{C= *}{RVS}{C= *}{OFF}";:NEXTJ
520 PRINT:PRINT"{RIGHT}{C= R} ";:GOSUB550:PRINT"{C= R}"
530 R$="{RVS}{C= U}{OFF}{C= E}":LL=LL+1:PRINT" {C= Z}";:GOSUB550:PRINT"{LEFT}{C= X}"
540 RETURN
550 FORL=1TOLL:PRINTR$;:NEXT:RETURN
560 POKE646,11:FORJ=1TO16:K=PT(RR,J):JJ=2+J*2
570 IFK>9THENL=INT(K/10):L$=MID$(STR$(L),2,1):GOTO590
580 L$=CHR$(32)
590 CX=JJ:CY=23:M$=L$:GOSUB1180:CX=JJ:CY=24:M$=RIGHT$(STR$(K),1):GOSUB1180
600 NEXTJ:RETURN
610 FORJ=0TO4:SY=4+J*4:FORK=0TOJ+3:SX=12-J*2+K*4
620 CX=SX+1:CY=SY-1:M$=" ":GOSUB1180
630 M$=" ":CX=SX+1:CY=SY-1:GOSUB1180:WP=INT(RND(1)*2)
640 SW(J,K,0)=WP:SW(J,K,1)=0:GOSUB760
650 NEXTK,J
660 FORJ=1TO8:POKE1074+J*2,48+J:NEXT
670 FORJ=0TO1:BX=J*31:CX=BX:CY=0:M$="":GOSUB1180
680 PRINT"{OFF}{BLK}{C= D}{RVS}{PUR}       {DOWN}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{RVS}{BLK}{C= K}{PUR}{C= D}{C= I}{C= I}{C= I}{C= I}{C= I}{C= F}";
690 FORK=1TO5:CX=BX:CY=K:M$="":GOSUB1180:PRINT"{RVS}{BLK}{C= K}{OFF}{PUR}{C= K}     {RVS}{C= K}":NEXT
700 CX=BX:CY=K:M$="":GOSUB1180
710 PRINT"{RVS}{BLK}{C= K}{PUR}{C= C}{OFF}{C= I}{C= I}{C= I}{C= I}{C= I}{RVS}{C= V}{DOWN}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{LEFT}{OFF}{BLK}{C= C}{RVS}{C= I}{C= I}{C= I}{C= I}{C= I}{C= I}{OFF}{C= V}"
720 NEXT:POKE646,4
730 CX=3+(LEN(P1$)=5):CY=0:M$="{RVS}"+P1$+"{OFF}":GOSUB1180
740 CX=34+(LEN(P2$)=5):CY=0:M$="{RVS}"+P2$+"{OFF}":GOSUB1180
750 RETURN
760 POKE646,1:CX=SX:CY=SY:M$=SP$(WP):GOSUB1180:RETURN 
770 FORJ=0TO32:LB(J,0)=0:NEXT:NB=1
780 POKE198,0:WAIT198,1:GETA$
790 IFA$="-"THENRETURN
800 IFA$="+"THENA$=STR$(INT(RND(1)*8+1))
810 A=VAL(A$):IF(A<1)OR(A>8)THEN780
820 LB(0,0)=1:FORJ=1TO3:LB(0,J)=0:NEXT:LB(0,4)=10+A*2
830 EX=1
840 FORJ=0TO32:IFLB(J,0)THENEX=0:GOSUB870
850 NEXT:IFEXTHENRETURN
860 GOTO830
870 DY=LB(J,0):DX=LB(J,1):LY=LB(J,2):NY=LB(J,3):NX=LB(J,4)
880 SM=1064+NX+LY*160+NY*40:IF(LY+NY)THENPOKESM,32
890 LB(J,3)=(NY+1)AND3:ONNY+1GOTO900,920,960,970
900 IFLY>4THENLB(J,0)=0:GOTO1030:REM SCORING ROUTINE
910 POKESM+40,81:ONINT(RND(1)*3+1)GOTO1080,1090,1100
920 VX=0:GOSUB1020:IF SW(WY,WX,1)=0OR(SW(WY,WX,0)=SD)=0THEN940
930 VX=1-2*SD:LB(J,1)=VX:LB(J,3)=NY+1:LB(J,4)=NX+VX:POKESM+40+VX,81:GOTO1120
940 IF SW(WY,WX,0)=SDTHENLB(J,0)=0:SW(WY,WX,1)=1:POKESM+40,81:GOTO1110
950 LB(J,3)=NY+1:POKESM+40,81:ONINT(RND(1)*3+1)GOTO1080,1090,1100
960 LB(J,1)=0:LB(J,4)=NX+DX:POKESM+40+DX,81:GOTO1130
970 LB(J,2)=LY+1:POKESM+40,81:GOSUB1020:SW(WY,WX,0)=1-SW(WY,WX,0)
980 IF SW(WY,WX,1)=0THEN1010
990 LB(NB,0)=1:LB(NB,1)=0:LB(NB,2)=LY:LB(NB,3)=0:LB(NB,4)=NX+2-SD*4:NB=NB+1
1000 SW(WY,WX,1)=0:POKESM-40+2-SD*4,32:GOSUB1140
1010 SX=12-WY*2+WX*4:SY=4+WY*4:WP=SW(WY,WX,0):GOSUB760:GOTO1120
1020 WY=LY:JX=(NX/2)+LY-6:WX=INT(JX/2):SD=JXAND1:RETURN
1030 SF=PT(RR,NX/2-1)
1040 SG=SC(QR,RR)+SF:POKE646,11
1050 TX=5+31*QR+(SG>9)+(SG>99)+(SG>999)
1060 TY=1+RR:A$=MID$(STR$(SG),2)
1070 CX=TX:CY=TY:M$=A$:GOSUB1180:SC(QR,RR)=SG:GOTO1150
1080 POKELB,48:POKEHB,4:POKEV,32:POKEV,33:RETURN
1090 POKELB,97:POKEHB,8:POKEV,32:POKEV,33:RETURN
1100 POKELB,152:POKEHB,5:POKEV,32:POKEV,33:RETURN
1110 POKEV,32:POKEV,33:FORA=50TO10STEP-1:POKEHB,A:NEXT:RETURN
1120 RETURN
1130 POKELB,152:POKEHB,10:POKEV,128:POKEV,129:RETURN
1140 POKELB+7,0:POKEHB+7,2:POKEV+7,128:POKEV+7,129:RETURN
1150 POKELB,195:POKEHB,16:POKELB+7,135:POKEHB+7,33:POKEV,32:POKEV,33:POKEV+7,32
1160 POKEV+7,33:RETURN
1170 REM CHAR COMMAND
1180 POKE783,0:POKE781,CY:POKE782,CX:SYS65520:PRINTM$;:RETURN
