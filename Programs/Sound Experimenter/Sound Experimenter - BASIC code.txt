100 POKE53280,6:PRINT"{CLR}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(9)"C64 SOUND EXPERIMENTS":FORT=1TO1300:NEXT
110 L1=15:W1=65:A1=25:D1=9:S1=0:W2$="PULSE":P1=2048:L2=15:F1$="NO":P2%=8
120 PRINT"{CLR}{DOWN}     {RVS}     SOUND EFFECT MENU     {OFF}{DOWN}{DOWN}{DOWN}{DOWN}"
130 PRINT" {RVS}1{OFF} WAVEFORM         {RVS}2{OFF} PULSE WIDTH {DOWN}
140 PRINT" {RVS}3{OFF} ATTACK/DECAY     {RVS}4{OFF} SUSTAIN/RELEASE {DOWN}
150 PRINT" {RVS}5{OFF} LOUDNESS         {RVS}6{OFF} SYNCHRONIZE V1TOV3
160 PRINT" {RVS}7{OFF} RING MODULATION  {RVS}8{OFF} FILTER/RESONANCE{DOWN}
170 PRINT" {RVS}9{OFF} MUSIC PLAYER     {RVS}10{OFF} ZERO VARIABLES {DOWN}
180 PRINT" {RVS}11{OFF} QUIT            {RVS}12{OFF} LIST OF EFFECTS{DOWN}"
190 INPUT"{DOWN}{DOWN}ENTER NUMBER OF YOUR SELECTION";A
200 ON A GOTO520,340,220,280,790,370,630,820,1370,1030,1670,1040
210 GOTO120
220 PRINT"{CLR}{DOWN}{DOWN}SET ATTACK VALUE BETWEEN 0 & 15"
230 PRINT"{DOWN}CURRENT VALUE OF ATTACK IS";(A1-D1)/16
240 INPUT"{DOWN}";A3:A2=A3*16
250 PRINT"{DOWN}{DOWN}SET DECAY VALUE BETWEEN 0 & 15"
260 PRINT"{DOWN}CURRENT VALUE OF DECAY IS";D1
270 INPUT"{DOWN}";D1:A1=A2+D1:GOTO120
280 PRINT"{CLR}{DOWN}{DOWN}SET SUSTAIN VALUE BETWEEN 0 & 15"
290 PRINT"{DOWN}CURRENT VALUE OF SUSTAIN IS";(S1-R1)/16
300 INPUT"{DOWN}";S3:S2=S3*16
310 PRINT"{DOWN}{DOWN}SET RELEASE VALUE BETWEEN 0 & 15"
320 PRINT"{DOWN}CURRENT VALUE OF RELEASE IS";R1
330 INPUT"{DOWN}";R1:S1=S2+R1:GOTO120
340 PRINT"{CLR}{DOWN}{DOWN}{DOWN}SELECT A PULSE WIDTH BETWEEN 0 & 4095"
350 PRINT"{DOWN}{DOWN}CURRENT PULSE WIDTH IS";P1
360 INPUT"{DOWN}";P1:P2%=P1/256:p3%=P1-256*P2%:GOTO120
370 PRINT"{CLR}{DOWN}{DOWN}{DOWN}DO YOU WANT SYNCHRONIZING?"
380 INPUT"TYPE Y OR N ";B$
390 IFB$="N"THENW1=W1AND253:S2=0:GOTO120
400 IFB$="Y"THENW1=(W1AND251)OR2:GOTO420
410 GOTO380
420 PRINT"{DOWN}{DOWN}PRESENT FREQUENCY OF V3(VOICE3)IS";V4
430 INPUT"{DOWN}{DOWN}ENTER FREQUENCY FOR V3";V4:V3=INT(V4*16.4)
440 V4%=V3/256:V5%=V3-(256*V4%)
450 PRINT"{DOWN}{DOWN} DO YOU WANT V3 TO BE OFFSET
460 PRINT"FROM FREQUENCY OF V1 BY"
470 PRINT"AMOUNT YOU JUST ENTERED?"
480 INPUT"{DOWN}TYPE Y OR N ";C$
490 IFC$="N"THENS2=0:GOTO120
500 IFC$="Y"THENS2=1
510 GOTO120
520 REM SET UP WAVEFORM TYPE
530 PRINT"{CLR}{DOWN}{DOWN}ENTER THE FIRST LETTER OF THE":PRINT"TYPE OF WAVEFORM YOU WANT:
540 PRINT"{DOWN}TRIANGLE, SAWTOOTH, PULSE, NOISE."
550 PRINT"{DOWN}WAVEFORM IS NOW SET TO ";W2$
560 INPUT"{DOWN}";W$
570 IFW$="T"THENW1=17:W2$="TRIANGLE"
580 IFW$="S"THENW1=33:W2$="SAWTOOTH"
590 IFW$="P"THENW1=65:W2$="PULSE"
600 IFW$="N"THENW1=129:W2$="NOISE"
610 IFW1=65GOTO340
620 GOTO120
630 PRINT"{CLR}{DOWN}{DOWN}IF YOU SELECT RING MODULATION THEN"
640 PRINT"WAVEFORM WILL BE SET TO TRIANGLE
650 INPUT"{DOWN}{DOWN}DO YOU WANT RING MOD? TYPE Y OR N";R$
660 IFR$="N"THENW1=W1AND113:S2=0:GOTO120
670 IFR$="Y"THENW1=21:W2$="TRIANGLE":GOTO690
680 GOTO630
690 PRINT"{DOWN}FREQUENCY OF V3(VOICE 3) IS NOW SET AT ";V4
700 INPUT"{DOWN}SET FREQUENCY OF V3 ";V4:V3=INT(V4*16.4)
710 V4%=V3/256:V5%=V3-(256*V4%)
720 PRINT"{DOWN}{DOWN}DO YOU WANT FREQ. OF V3 TO"
730 PRINT"TO BE OFFSET FROM V1 BY"
740 PRINT"AMOUNT YOU JUST ENTERED"
750 INPUT"{DOWN}{DOWN}TYPE Y OR N ";O$
760 IF O$="N"THEN S2=0:GOTO120
770 IF O$="Y"THEN S2=1
780 GOTO120
790 PRINT"{CLR}{DOWN}{DOWN}LOUDNESS IS NOW SET AT";L1AND15
800 INPUT"{DOWN}{DOWN}SET LOUDNESS BETWEEN 0 & 15";L2
810 L1=L1AND240:L1=L2+L1:GOTO120
820 PRINT"{CLR}{DOWN}{DOWN}ENTER FIRST LETTER OF TYPE":PRINT"OF FILTERING YOU WANT:
830 PRINT"{DOWN}LOWPASS, BANDPASS, HIGHPASS,
840 PRINT"OR NOTCH FILTER {DOWN}"
850 PRINTF1$;" FILTERING IS BEING USED NOW.
860 INPUT"{DOWN}TO TURN FILTERING OFF ENTER 0";F$
870 IFF$="0"THENL1=L1AND15:R2=0:F1$="NO":R6=0
880 IFF$="N"THENL1=L2+80:R2=1:F1$="NOTCH"
890 IFF$="L"THENL1=L2+16:R2=1:F1$="LOWPASS"
900 IFF$="B"THEN L1=L2+32:R2=1:F1$="BANDPASS"
910 IFF$="H"THEN L1=L2+64:R2=1:F1$="HIGHPASS"
920 PRINT"{CLR}{DOWN}{DOWN}CUTOFF FREQUENCY IS NOW SET AT";F
930 PRINT"{DOWN}{DOWN}INPUT CUTOFF FREQUENCY
940 INPUT"BETWEEN 0 & 2047 CYCLES";F
950 F1%=F/8:F2%=F-(8*F1%):F$=""
960 PRINT"{DOWN}{DOWN}DO YOU WANT TO SET RESONANCE
970 INPUT"TYPE Y OR N";R$
980 IFR$="Y"GOTO1000
990 R6=1:R5=0:GOTO120
1000 PRINT"{DOWN}RESONANCE IS NOW SET AT";R5
1010 PRINT"{DOWN}{DOWN}SET RESONANCE BETWEEN 0 & 15
1020 INPUTR5:R6=R5*16+1:GOTO120
1030 CLR:F1$="NO":GOTO120
1040 REM PRINT LIST OF SOUND EFFECTS
1050 PRINT"{CLR}{DOWN}WAVEFORM TYPE IS ";W2$
1060 IF W2$="PULSE"THEN PRINT"PULSEWIDTH IS ";P1
1070 PRINT"{DOWN}ATTACK IS ";(A1-D1)/16;"  DECAY IS ";D1
1080 PRINT"{DOWN}SUSTAIN IS ";(S1-R1)/16;" RELEASE IS ";R1
1090 PRINT"{DOWN}LOUDNESS IS SET TO ";L1AND15
1100 IFR2=0GOTO1140
1110 PRINT"{DOWN}"F1$" FILTER BEING USED WITH
1120 PRINT"CUTOFF FREQUENCY OF ";F
1130 PRINT"{DOWN}RESONANCE IS ";R5
1140 IFW1AND2THENPRINT"{DOWN}SYNCHRONIZING IS ON":GOTO1170
1150 IFW1AND4THENPRINT"{DOWN}RING MODULATION IS ON":GOTO1170
1160 GOTO1190
1170 PRINT"POKE54286 WITH";V5%;"& POKE54287 WITH";V4%;
1180 PRINT"    TO SET V3 FREQ TO";V4;"HZ
1190 IFS2=1THENPRINT"FREQUENCY OFFSET IS BEING USED.
1200 IFS2=1THENPRINT"SEE PROGRAM LINES 1640-1660
1210 INPUT"{DOWN}PRESS RETURN TO CONTINUE";R8
1220 PRINT"{CLR}{DOWN}VOICE 1{RIGHT}{RIGHT}{RIGHT}VOICE 2{RIGHT}{RIGHT}{RIGHT}VOICE 3{RIGHT}{RIGHT}{RIGHT}VALUE TO {DOWN}"
1230 PRINT"REGISTER{RIGHT}{RIGHT}REGISTER{RIGHT}{RIGHT}REGISTER{RIGHT}{RIGHT}BE POKED{DOWN}"
1240 S(0)=P3%:S(1)=P2%:S(2)=W1:S(3)=A1:S(4)=S1
1250 FORS=0TO4
1260 PRINT 54274+S,54281+S,54288+S,S(S):NEXT
1270 PRINT"{DOWN}{DOWN}FILTERING & LOUDNESS"
1280 PRINT 54293;TAB(30);F2%
1290 PRINT 54294;TAB(30);F1%
1300 PRINT 54296;TAB(30);L1
1310 PRINT"{DOWN}RESONANCE & VOICE TO BE FILTERED
1320 PRINT"54295";TAB(30);R6AND240
1330 PRINT"ADD 1 TO 54295 FOR FILTERING VOICE 1
1340 PRINT"ADD 2 TO 54295 FOR FILTERING VOICE 2
1350 PRINT"ADD 4 TO 54295 FOR FILTERING VOICE 3
1360 INPUT"PRESS RETURN TO CONTINUE ";R8:GOTO120
1370 RE=54272:RESTORE
1380 FORR=54272 TO 54296:POKER,0:NEXT
1390 REM ENTER SOUND EFFECTS INTO SID
1400 POKERE+2,P3%:POKERE+3,P2%
1410 POKERE+5,A1:POKERE+6,S1:POKERE+14,V5%
1420 POKERE+15,V4%:POKERE+21,F2%:POKERE+22,F1%
1430 POKERE+23,R2:POKERE+24,L1
1440 READN,D:IFN=0THENPOKERE+24,0:GOTO120:REM PLAY MUSIC
1450 H=INT(N/256):L=N-(256*H):DUR=1600/D
1460 POKERE+1,H:POKERE,L:POKERE+4,W1
1470 IF S2=1 GOTO1630
1480 FOR T=1TODUR:NEXT
1490 IFW1<1GOTO1510
1500 POKERE+4,W1-1
1510 FORT=1TO20:NEXT:GOTO1440
1520 DATA10814,8,11457,8
1530 DATA12860,4,12860,16,14435,8,17167,8,10814,4,11457,8,10814,8
1540 DATA9634,4,9634,16,11457,8,8101,8,8583,4
1550 DATA10814,8,11457,8
1560 DATA12860,4,12860,16,14435,8,17167,8,10814,4,11457,8,10814,8
1570 DATA9634,4,9634,16,11457,8,8101,8,8583,4
1580 DATA12860,4,17167,16
1590 DATA21269,4,21269,16,21269,8,19269,8,17167,4,12860,8,12860,8
1600 DATA14435,4,17167,16,17167,8,14435,8,12860,4,10814,4
1610 DATA10814,4,10814,16,10814,8,9634,8,10814,4,12860,8,10814,8
1620 DATA9634,4,9634,16,11457,8,8101,8,8583,4,0,0
1630 REM DEVELOP OFFSET FREQUENCY
1640 F1=H*256+L:F1=F1-V3:IFF1<0THENF1=0
1650 V4%=F1/256:V5%=F1-(256*V4%)
1660 POKERE+14,V5%:POKERE+15,V4%:GOTO1480
1670 END
