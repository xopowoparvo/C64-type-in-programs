100 POKE53280,0:POKE53281,2:PRINT"{CLR}{DOWN}{DOWN}{DOWN}{WHT}"TAB(15)"64 DOODLER"
110 PRINT"{DOWN}{DOWN}{BLK}"TAB(8)"PLUG JOYSTICK INTO PORT 2"
120 PRINT"{DOWN}"TAB(9)"USE IT TO DRAW PICTURES"
130 PRINT"{DOWN}"TAB(10)"TRIGGER CLEARS SCREEN":PRINT"{DOWN}{DOWN}{DOWN}{DOWN}{WHT}"TAB(14)"PRESS ANY KEY"
140 GETA$:IFA$=""THEN140
150 PRINT"{CLR}":FORI=0TO7:READP(I):NEXT:DATA1,2,4,8,16,32,64,128
160 FORI=0TO10:READDX(I),DY(I):NEXT
170 DATA0,0,0,-1,0,1,0,0,-1,0,-1,-1,-1,1,0,0,1,0,1,-1,1,1
180 POKE53272,PEEK(53272)OR8:POKE53265,PEEK(53265)OR32
190 GOSUB300:FORI=1024TO2023:POKEI,2:NEXT
200 X=160:Y=100
210 GOSUB280:J=PEEK(56320):K=JAND16:IFK=0THENGOSUB300
220 J=JAND15:J=15-J:X=X+DX(J):Y=Y+DY(J)
230 IFX>319THENX=0
240 IFX<0THENX=319
250 IFY>199THENY=0
260 IFY<0THENY=199
270 GOTO210
280 RO%=Y/8:CH%=X/8:LI=YAND7:BI=XAND7:BI=7-BI:B1=RO%*320:B2=CH%*8
290 BY=8192+B1+B2+LI:POKEBY,PEEK(BY)ORP(BI):RETURN
300 FORI=8192TO16383:POKEI,0:NEXT:RETURN
